apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = GROUP
version = VERSION_NAME

android {
  compileSdkVersion Versions.compileSdkVersion

  resourcePrefix 'magellan_'

  defaultConfig {
    minSdkVersion Versions.minSdkVersion
    targetSdkVersion Versions.targetSdkVersion

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    setSourceCompatibility(JavaVersion.VERSION_17)
    setTargetCompatibility(JavaVersion.VERSION_17)
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  buildFeatures {
    viewBinding = true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  namespace 'com.wealthfront.magellan'

  publishing {
    singleVariant("release") {
      withSourcesJar()
    }
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  if (!name.contains("UnitTest")) {
    kotlinOptions.freeCompilerArgs = ['-Xexplicit-api=strict', '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi']
  } else {
    kotlinOptions.freeCompilerArgs = ['-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi,kotlinx.coroutines.InternalCoroutinesApi']
  }
  kotlinOptions.allWarningsAsErrors = true
  kotlinOptions.jvmTarget = JavaVersion.VERSION_17
}

dependencies {
  implementation libs.appCompat
  implementation libs.lifecycle
  implementation libs.inject
  implementation libs.coroutines
  implementation libs.coroutinesAndroid

  testImplementation project(':internal-test-support')
  testImplementation libs.testCore
  testImplementation libs.testCoreKtx
  testImplementation libs.junit
  testImplementation libs.extJunitKtx
  testImplementation libs.truth
  testImplementation libs.mockito
  testImplementation libs.mockK
  testImplementation libs.archTesting
  testImplementation libs.robolectric
  testImplementation libs.coroutinesTest

// Bug in AGP: Follow this issue - https://issuetracker.google.com/issues/141840950
// lintPublish project(':magellan-lint')
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
